float DistributionGGX(vec3 N, vec3 H, float roughness)
{
    float a = roughness * roughness;
    float a2 = a * a;
    float NdotH = max(dot(N, H), 0.0);
    float NdotH2 = NdotH * NdotH;

    float nom = a2;
    float denom = (NdotH2 * (a2 - 1.0) + 1.0);
    denom = PI * denom * denom;

    return nom / denom;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
    float r = (roughness + 1.0);
    float k = (r * r) / 8.0;

    float nom = NdotV;
    float denom = NdotV * (1.0 - k) + k;

    return nom / denom;
}

float GeometrySmith(vec3 N, vec3 V, vec3 L, float roughness)
{
    float NdotV = max(dot(N, V), 0.0);
    float NdotL = max(dot(N, L), 0.0);
    float ggx = GeometrySchlickGGX(NdotL, roughness);
    float ggx2 = GeometrySchlickGGX(NdotV, roughness);

    return ggx * ggx2;
}

vec3 FresnelSchlick(float cosTheta, vec3 F0)
{
    return F0 + (1.0 - F0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

float CalculateShadowDepth(vec3 fragPos, float farPlane, int lightIndex)
{
	vec3 fragToLight = fragPos - uLights.Lights[lightIndex].Position.xyz;

	float distance = length(fragToLight) / farPlane;
	float closestDepth = texture(uShadowMaps[lightIndex], fragToLight).r;

	return closestDepth;
}

float CalculateShadow(vec3 fragPos, float farPlane, int lightIndex)
{
	vec3 fragToLight = fragPos - uLights.Lights[lightIndex].Position.xyz;

	float distance = length(fragToLight) / farPlane;
	float closestDepth = texture(uShadowMaps[lightIndex], fragToLight).r;

	float bias = 0.015;
	float shadow = closestDepth + bias < distance ? 0.15 : 1.0;

	return shadow;
}
